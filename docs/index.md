---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: 'Nvidia GPU Guardian Provider'
subcategory: ''
description: |-
---

# Nvidia GPU Guardian Provider

Nvidia GPU Guardian provides real-time, automatic remediation for cloud operations.

_Automatic remediation_ centralizes auto-healing processes by preventing or fixing repetitive incidents without human intervention.

The Nvidia GPU Guardian Provider and associated Op Packs are remediation as code tools. You can easily integrate Nvidia GPU Guardian's automated monitoring, debugging, and repairing capabilities within your Terraform configuration.

-> _Remediation as code_ adds auto-remediation tool creation and management to the same high-level, codified pipeline as the rest of your operations configuration.

Check out Getting Started with Nvidia GPU Guardian for more details!

## Example Usage

The following configuration creates a basic auto-remediation loop within Nvidia GPU Guardian via Terraform:

- The Alarm defines when there is an issue.
- The Action defines what to do about the issue.
- The Bot connects the two by triggering the Action when the Alarm fires.

```terraform
# Defines the required Nvidia GPU Guardian provider and version
terraform {
  required_providers {
    ngg = {
      source  = "nvidia/ngg"
      version = ">= 1.0.7"
    }
  }
}

# Set provider-specific arguments
provider "ngg" {
  url = "<CLUSTER_API_ENDPOINT>"
}

# Create an Alarm that fires when host CPU usage exceeds 35% for 48 of the previous 60 seconds.
# This Alarm clears when CPU usage is below 35% for the previous 180 seconds.
resource "ngg_alarm" "high_cpu_alarm" {
  name                   = "high_cpu_alarm"
  fire_query             = "(cpu_usage > 35 | sum(60)) >= 48"
  clear_query            = "(cpu_usage < 35 | sum(180)) >= 180"
  description            = "Watch CPU usage."
  resource_query         = "hosts"
  enabled                = true
  resolve_short_template = "high_cpu_alarm resolved"
}

# Create an Action that executes a Linux command to count the active background jobs.
resource "ngg_action" "background_jobs_action" {
  name                    = "background_jobs_action"
  command                 = "`top -b -n 1 | head -n 15`"
  description             = "Count background jobs"
  start_title_template    = "background_jobs_action started"
  complete_title_template = "background_jobs_action completed"
  error_title_template    = "background_jobs_action failed"
  enabled                 = true
}

# Create a Bot that triggers the 'background_jobs_action' when the 'high_cpu_alarm' fires.
resource "ngg_bot" "cpu_bot" {
  name        = "cpu_bot"
  command     = "if ${ngg_alarm.high_cpu_alarm.name} then ${ngg_action.background_jobs_action.name} fi"
  description = "Count background jobs on high CPU usage."
  enabled     = true
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `debug` (Boolean) Debug logging to `/tmp/tf_provider.log`.
- `min_version` (String) Minimum version required on the Nvidia GPU Guardian backend (API server).
- `retries` (Number) Number of retries for API calls, in case of e.g. transient network failures.
- `token` (String, Sensitive) Customer/user-specific authorization token for the Nvidia GPU Guardian API server. May be provided via `NGG_TOKEN` env variable.
- `url` (String) Customer-specific URL for the Nvidia GPU Guardian API server.
